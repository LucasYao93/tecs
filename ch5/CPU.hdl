// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/05/CPU.hdl

/**
 * The Central Processing unit (CPU).
 * Consists of an ALU and a set of registers, designed to fetch and 
 * execute instructions written in the Hack machine language.
 * In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM=0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time unit. If reset=1 then the 
 * CPU jumps to address 0 (i.e. sets pc=0 in next time unit) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset=1) or continue executing
                         // the current program (reset=0).

    OUT outM[16],        // M value output
        writeM,          // Write into M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    Not(in=instruction[15],out=ni15);
    
    Or(a=ni15,b=instruction[5],out=aload);
    
    Mux16(a=aluout,b=instruction,sel=ni15,out=ain);

    ARegister(in=ain,load=aload,out=aout);
    
    And(a=instruction[4],b=instruction[15],out=bload);
    DRegister(in=aluout,load=bload,out=dout);

    Mux16(a=aout,b=inM,sel=instruction[12],out=amout);

    ALU(x=dout,y=amout,zx=instruction[11],nx=instruction[10],zy=instruction[9],ny=instruction[8],f=instruction[7],no=instruction[6],out=aluout,zr=zr,ng=ng);
    And(a=instruction[3],b=instruction[15],out=writeM);
    And16(a=aluout,b=aluout,out=outM);
    And16(a=aout,b=aout,out[0..14]=addressM);
    Not(in=jump,out=notjump);
    
    And(a=instruction[1],b=zr,out=j1);
    And(a=instruction[2],b=ng,out=j2);
    
    Not(in=zr,out=nzr);
    Not(in=ng,out=nng);
    And(a=nzr,b=nng,out=pos);
    
    And(a=instruction[0],b=pos,out=j3);
    
    Or(a=j1,b=j2,out=jj);
    Or(a=j3,b=jj,out=jor);
    And(a=jor,b=instruction[15],out=jump);
    
    PC(in=aout,load=jump,inc=notjump,reset=reset,out[0..14]=pc);    

}

